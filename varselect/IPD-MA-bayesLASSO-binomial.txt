model{

########## IPD-MA model
for (i in 1:Np){
	y[i] ~ dbern(p[i])  
	logit(p[i]) <- a[studyid[i]] + inprod(b[studyid[i],], X[i,]) +
	 (1 - equals(treat[i],1)) * inprod(c[studyid[i],], X[i,]) + d[studyid[i],treat[i]] 
}

#####treatment effect
for(j in 1:Nstudies){
	d[j,1] <- 0
	d[j,2] ~ dnorm(delta[2], tau)
}

## random effects for prognostic effect
for(j in 1:Nstudies){
	for(k in 1:Ncovariate){
		b[j,k] ~ dnorm(beta[k], taustar)
	}
}

## random effects for effect modification
for(j in 1:Nstudies){
	for(k in 1:Ncovariate){
		c[j,k] ~ dnorm(gamma[k], taustar)
	}
}

## prior distribution for heterogeneity of the treatment effect
tau <- pow(sd, -2)
sd ~ dnorm(0,1)T(0,)

## prior distribution for heterogeneity of the effect modifiers and covariates
taustar <- pow(sdstar, -2)
sdstar ~ dnorm(0,1)T(0,)


## prior distribution for average treatment effect
delta[1] <- 0
delta[2] ~ dnorm(0, 0.001)

## prior distribution for baseline effect
for (j in 1:Nstudies){
	a[j] ~ dnorm(0, 0.001)	
}

#lambda ~ dgamma(0.001, 0.001)
lambda ~ dgamma(2, 0.1)

for(k in 1:Ncovariate){
	beta[k] ~ dnorm(0, 0.001)
}

for(k in 1:Ncovariate){
	gamma[k] ~ ddexp(0, lambda) # penalization is only on gamma
}
}

