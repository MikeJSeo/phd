model{

########## IPD-MA model
for (i in 1:Np){
	y[i] ~ dbern(p[i])
	logit(p[i]) <- alpha[studyid[i]] + inprod(beta[], X[i,]) + 
	delta[studyid[i],treat[i]] + (1 - equals(treat[i],1)) * inprod(g[], X[i,])
}

#####treatment effect
for(i in 1:Nstudies){
	delta[i,1] <- 0
	delta[i,2] ~ dnorm(d[2], tauDelta)
}

## prior distribution for heterogeneity of the treatment effect
tauDelta <- pow(sdDelta, -2)
sdDelta ~ dunif(0, 5)

## prior distribution for treatment effect
d[1] <- 0
d[2] ~ dnorm(0, 0.001)

for (i in 1:Nstudies){
	alpha[i] ~ dnorm(0, 0.001)	
}

for(i in 1:Ncovariate){
	IndA[i] ~ dcat(Pind[])
	Ind[i] <- IndA[i] - 1
	beta[i] ~ dnorm(0, TauM[IndA[i]])
}

for(i in 1:Ncovariate){
	IndA2[i] ~ dcat(Pind[])
	Ind2[i] <- IndA2[i] - 1
	g[i] ~ dnorm(0, TauM[IndA2[i]])
}

tauBeta <- pow(sdBeta, -2)
sdBeta ~ dunif(0, 5)
TauM[1]<- tauBeta * 100
TauM[2] <- tauBeta

Pind[1] <- 0.5
Pind[2] <- 0.5


}

