model{

########## IPD-MA model
for (i in 1:Np){
	y[i] ~ dnorm(mu[i], tau)  
	mu[i]<-alpha[studyid[i]] + inprod(beta[], X[i,]) + 
	delta[studyid[i],treat[i]] + (1 - equals(treat[i],1)) * inprod(g[], X[i,])
}
tau ~ dgamma(0.001, 0.001)

#####treatment effect
for(i in 1:Nstudies){
	delta[i,1] <- 0
	delta[i,2] ~ dnorm(d[2], tauDelta)
}

## prior distribution for heterogeneity of the treatment effect
tauDelta <- pow(sdDelta, -2)
sdDelta ~ dnorm(0, 1)T(0,)

## prior distribution for treatment effect
d[1] <- 0
d[2] ~ dnorm(0, 0.001)


for (i in 1:Nstudies){
	alpha[i] ~ dnorm(0, 0.001)	
}

for(i in 1:Ncovariate){
	#IndA[i] ~ dcat(Pind[])
	#Ind[i] <- IndA[i] - 1
	#beta[i] ~ dnorm(0, tauCov[IndA[i]])
	beta[i] ~ dnorm(0, 0.001)
}

for(i in 1:Ncovariate){
	IndA2[i] ~ dcat(Pind[])
	Ind2[i] <- IndA2[i] - 1
	g[i] ~ dnorm(0, tauCov[IndA2[i]])
}

zeta <- pow(eta, -2)
eta ~ dunif(0, 5)
tauCov[1] <- zeta
tauCov[2] <- zeta * 0.01  # g = 100

Pind[1] <- 0.5 #P(I_j=1)= 0.5
Pind[2] <- 0.5 

}

