model{

########## IPD-MA model
for (i in 1:Np){
	y[i] ~ dnorm(mu[i], tau)  
	mu[i]<-alpha[studyid[i]] + inprod(beta[], X[i,]) + 
	delta[studyid[i],treat[i]] + (1 - equals(treat[i],1)) * inprod(b[studyid[i],], X[i,])
}
tau ~ dgamma(0.001, 0.001)

#####treatment effect
for(j in 1:Nstudies){
	delta[j,1] <- 0
	delta[j,2] ~ dnorm(d[2], tauDelta)
}

for(j in 1:Nstudies){
	for(k in 1:Ncovariate){
		b[j,k] ~ dnorm(g[k], tauB)
	}
}

## prior distribution for heterogeneity of the treatment effect
tauDelta <- pow(sdDelta, -2)
sdDelta ~ dnorm(0,1)T(0,)

## prior distribution for heterogeneity of the effect modifiers and covariates
tauB <- pow(sdB, -2)
sdB ~ dnorm(0,1)T(0,)


## prior distribution for average treatment effect
d[1] <- 0
d[2] ~ dnorm(0, 0.001)


## prior distribution for baseline effect
for (i in 1:Nstudies){
	alpha[i] ~ dnorm(0, 0.001)	
}

tt <- lambda #*tau # uses conditional prior
lambda ~ dgamma(2, 0.1) #dgamma(2, 0.1)# this diffuse prior has mean 20, variance 200 

for(k in 1:Ncovariate){
	beta[k] ~ dnorm(0, 0.001)
}

for(k in 1:Ncovariate){
	g[k] ~ ddexp(0, tt)
}
}

