model{

########## IPD-MA model
for (i in 1:Np){
	y[i] ~ dnorm(mu[i], tau)  
	mu[i]<-alpha[studyid[i]] + inprod(beta[], X[i,]) + 
	delta[studyid[i],treat[i]] + (1 - equals(treat[i],1)) * inprod(g[], X[i,])
}
tau ~ dgamma(0.001, 0.001)

#####treatment effect
for(i in 1:Nstudies){
	delta[i,1] <- 0
	delta[i,2] ~ dnorm(d[2], tauDelta)
}

## prior distribution for heterogeneity of the treatment effect
tauDelta <- pow(sdDelta, -2)
sdDelta ~ dnorm(0, 1)T(0,)

## prior distribution for treatment effect
d[1] <- 0
d[2] ~ dnorm(0, 0.001)

for (i in 1:Nstudies){
	alpha[i] ~ dnorm(0, 0.001)	
}

tt <- lambda*tau #lambda value has to be specified
lambda ~ dgamma(2,0.1) # this diffuse prior has mean 20, variance 200 

for(i in 1:Ncovariate){
	beta[i] ~ dnorm(0, 0.001) #ddexp(0, tt)
	#Ind1[i] <- step(abs(beta[i]) - 0.05)
}

for(i in 1:Ncovariate){
	g[i] ~ ddexp(0, tt)
	#Ind2[i] <- step(abs(g[i]) - 0.05)
}
}

