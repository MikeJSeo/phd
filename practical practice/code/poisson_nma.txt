model {
	for(i in 1:ns) {                                              
##binomial likelihood of number of events for each arm k of study i		     
		for (k in 1:2) {r[i,k] ~ dpois(gamma[i,k]) 
		gamma[i,k] <- lambda[i,k]*E[i,k]
		}
		

##parameterization of the 'true' effect of each study i               
		 log(lambda[i,1])<- u[i]		
     		 log(lambda[i,2])<- u[i] + theta[i]

##distribution of random effects
		theta[i] ~ dnorm(mean[i],prec)
		
##consistency equations             
      mean[i]<-  d[t[i,2]] - d[t[i,1]]}

##prior distribution for log-odds in baseline arm of study i
	for (i in 1:ns) {u[i] ~ dnorm(0,.01)}	

##prior distribution for heterogeneity		
	tau ~ dnorm(0,1)I(0,)
	prec <- 1/tau.sq
	tau.sq <- pow(tau,2)
	
##prior distribution for basic parameters
	d[ref] <- 0

	for(k in 1:(ref-1)) {d[k] ~ dnorm(0,.01)}
	for(k in (ref+1):nt) {d[k] ~ dnorm(0,.01)}

##OR for each comparison of treatment i vs. j		
	for(i in 1:(nt-1)) {
		for (j in (i+1):nt) {
			OR[j,i]<- exp(d[j] - d[i])
			LOR[j,i]<- d[j] - d[i]}}

for(j in 1:(ref-1)){ORref[j]<- exp(d[j] - d[ref])}
for(j in (ref+1):nt) {ORref[j]<- exp(d[j] - d[ref])}
}