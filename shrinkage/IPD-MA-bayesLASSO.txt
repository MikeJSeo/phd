model{

########## IPD-MA model
for (i in 1:Np){
	y[i] ~ dnorm(mu[i], sigma)  
	mu[i]<-a[studyid[i]] + inprod(beta[], X[i,]) +
	 (1 - equals(treat[i],1)) * inprod(gamma[], X[i,]) + d[studyid[i],treat[i]] 
}
sigma ~ dgamma(0.001, 0.001)

#####treatment effect
for(j in 1:Nstudies){
	d[j,1] <- 0
	d[j,2] ~ dnorm(delta[2], tau)
}

## prior distribution for heterogeneity of the treatment effect
tau <- pow(sd, -2)
sd ~ dnorm(0,1)T(0,)

## prior distribution for average treatment effect
delta[1] <- 0
delta[2] ~ dnorm(0, 0.001)


## prior distribution for baseline effect
for (j in 1:Nstudies){
	a[j] ~ dnorm(0, 0.001)	
}

tt <- lambda * sigma  # uses conditional prior
lambda ~ dgamma(2, 0.1) # this diffuse prior has mean 20, variance 200 

for(k in 1:Ncovariate){
	beta[k] ~ dnorm(0, 0.001)
}

for(k in 1:Ncovariate){
	gamma[k] ~ ddexp(0, tt) # penalization is only on gamma
}
}

