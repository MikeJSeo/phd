model{

########## IPD-MA model
for (i in 1:N){
	y[i] ~ dnorm(mu[i], tau)  
	mu[i] <- a + inprod(b[], X[patientid[i],]) + 
		inprod(c[,treat[patientid[i]]], X[patientid[i],]) + d[treat[patientid[i]]] +
		e * time[i] + f[treat[patientid[i]]] * time[i] +
		v[patientid[i],1] + v[patientid[i],2]*time[i]			
}
tau <- pow(sigma, -2)
sigma ~ dunif(0,100)


## prior distribution for the intercept
a ~ dnorm(0, 0.001)

for(k in 1:Ncovariate){
	b[k] ~ dnorm(0, 0.001)
}

for(k in 1:Ncovariate){
	c[k,1] <- 0
}

for(k in 1:Ncovariate){
	for(t in 2:Ntreat){
		c[k,t] ~ dnorm(0,0.001)
	}
}

#####treatment effect
d[1] <- 0
for(t in 2:Ntreat){
	d[t] ~ dnorm(0, 0.001)
}

e ~ dnorm(0, 0.001)

f[1] <- 0
for(t in 2:Ntreat){
	f[t] ~ dnorm(0, 0.001)
}


# random intercept and slope
for( j in 1:Npatient){
	v[j,1:2] ~ dmnorm(zero.v, invSigma.v)
}

zero.v[1] <- 0
zero.v[2] <- 0

R.v[1,1] <- pow(sigma.v0,2)
R.v[2,2] <- pow(sigma.v1,2)
R.v[1,2] <- rho * sigma.v0 * sigma.v1
R.v[2,1] <- R.v[1,2]
invSigma.v ~ dwish(R.v,2)
Sigma.v <- inverse(invSigma.v)

#hyperpriors
sigma.v0 ~ dunif(0,10)
sigma.v1 ~ dunif(0,10)
rho ~ dunif(-1,1)


}